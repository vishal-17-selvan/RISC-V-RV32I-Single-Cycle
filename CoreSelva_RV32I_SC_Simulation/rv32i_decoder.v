`timescale 1ns/1ps

// CoreSelva RV32I_SC
// Author: Vishal Selavn
// Instruction decoder for the single-cycle RV32I core.

// Decode stage: extracts register indices, function fields and the
// sign-extended immediate for each RV32I instruction format.
module rv32i_decoder(
    input wire [31:0] instr,
    output wire [6:0] opcode,
    output reg [4:0] rd,
    output reg [2:0] funct3,
    output reg [4:0] rs1,
    output reg [4:0] rs2,
    output reg [6:0] funct7,
    output reg [31:0] imm );
assign opcode = instr[6:0];
always @(*) begin
    // Safe defaults prevent inferred latches for unsupported opcodes.
    rd = 5'd0;
    funct3 = 3'd0;
    rs1 = 5'd0;
    rs2 = 5'd0;
    funct7 = 7'd0;
    imm = 32'd0;
    case(opcode)
        // R-type register-register ALU instruction.
        7'b0110011: begin
            rd = instr[11:7];
            funct3 = instr[14:12];
            rs1 = instr[19:15];
            rs2 = instr[24:20];
            funct7 = instr[31:25];
        end
        // I-type ALU instruction. funct7 distinguishes SRLI from SRAI.
        7'b0010011: begin
            rd = instr[11:7];
            funct3 = instr[14:12];
            rs1 = instr[19:15];
            funct7 = instr[31:25];
            imm = {{20{instr[31]}},instr[31:20]};
        end
        // Other I-type encodings: loads, JALR and SYSTEM.
        7'b0000011, 7'b1100111, 7'b1110011: begin
            rd = instr[11:7];
            funct3 = instr[14:12];
            rs1 = instr[19:15];
            imm = {{20{instr[31]}},instr[31:20]};
        end
        // S-type store immediate.
        7'b0100011: begin
            funct3 = instr[14:12];
            rs1 = instr[19:15];
            rs2 = instr[24:20];
            imm = {{20{instr[31]}},instr[31:25],instr[11:7]};
        end
        // B-type branch immediate, including the implicit low zero bit.
        7'b1100011: begin
            funct3 = instr[14:12];
            rs1 = instr[19:15];
            rs2 = instr[24:20];
            imm = {
                {19{instr[31]}},
                instr[31],
                instr[7],
                instr[30:25],
                instr[11:8],
                1'b0
            };
        end
        // U-type immediate used by LUI and AUIPC.
        7'b0110111, 7'b0010111: begin
            rd = instr[11:7];
            imm = {instr[31:12],12'b0};
        end
        // J-type JAL immediate, including the implicit low zero bit.
        7'b1101111: begin
            rd = instr[11:7];
            imm = {
                {11{instr[31]}},
                instr[31],
                instr[19:12],
                instr[20],
                instr[30:21],
                1'b0
            };
        end
        default: ;
    endcase
end
endmodule
